// 2019-01-07 22:08:11
#event create THANOS CAR
/// THANOS CAR
time = 0;
mouse_xprev = -1;
mouse_yprev = -1;

// screens
WIN_WID = room_width;
WIN_HEI = room_height;
RES_WID = WIN_WID;
RES_HEI = WIN_HEI;

// 불 노이즈 텍스쳐; 이 텍스쳐를 바꾸시면 불의 형대가 달라집니다.
texCoolingMap = sprite_get_texture(texPerlin, 0);

// 불이 그려지는 서피스입니다. 여기에 흰색으로 무언갈 그리면 그게 불이 됩니다.
// 이 예제에서는 화면 전체를 차지하게 하도록 했습니다. 사이즈는 화면의 1/2 이고요.
surfFire = surface_create(RES_WID, RES_HEI);
texFire = surface_get_texture(surfFire);

// 그리고 이건 그냥 서피스. 이 서피스를 불이 그려지는 서피스에 그립니다.
surfGame = surface_create(RES_WID, RES_HEI);

// 쉐이더 변수
uCoolingMap = shader_get_sampler_index(shd_fire, "u_coolingmap");
uSource = shader_get_sampler_index(shd_fire, "u_source");
uTextureSize = shader_get_uniform(shd_fire, "u_texturesize");
uTime = shader_get_uniform(shd_fire, "u_time");
uScrollSpeed = shader_get_uniform(shd_fire, "u_scrollspeed");
uWindStrength = shader_get_uniform(shd_fire, "u_windstrength");
uWindSpeed = shader_get_uniform(shd_fire, "u_windspeed");

uBurnIntensity = shader_get_uniform(shd_burn, "u_intensity");
uBurnMap = shader_get_sampler_index(shd_burn, "u_texture");
uBurnTime = shader_get_sampler_index(shd_burn, "u_time");

// HD
texture_set_interpolation(true);
    
// if (display_aa > 12)
//     display_reset(8, false);
// else if (display_aa > 6)
//     display_reset(4, false);
// else
//     display_reset(2, false);

draw_set_font(fntHangul);

#event step 서피스 살려두기 & 시간 변수 증가
if (!surface_exists(surfFire))
{
    surfFire = surface_create(RES_WID, RES_HEI);
    texFire = surface_get_texture(surfFire);
}

if (!surface_exists(surfGame))
    surfGame = surface_create(RES_WID, RES_HEI);

// 시간
time += 2;

#section 다음 룸으로 넘어가기
if (keyboard_check_pressed(vk_enter))
    room_goto(rm_cartoon);

#event other_room_end
if (surface_exists(surfFire))
    surface_free(surfFire);
    
if (surface_exists(surfGame))
    surface_free(surfGame);

#event draw 일반 서피스 그리기
surface_set_target(surfGame);
draw_clear_alpha(0, 0);

// 매 프레임마다 맨 아래줄에 흰 픽셀을 넣어서 불이 계속 "일어나게" 합니다
var _hei = 4;
var _fireblue = $ffe5db;
draw_rectangle_colour(0, RES_HEI - _hei, RES_WID, RES_HEI, _fireblue, _fireblue, _fireblue, _fireblue, false);

// oof ow my bones ow
/*
draw_set_halign(1);draw_set_valign(1);
draw_set_font(fntBonehurtingjuice);
var _rainbow = make_colour_hsv(time * 12, 255, 255);
draw_text_colour(RES_WID >> 1, RES_HEI >> 1, "H", _rainbow, _rainbow, _rainbow, _rainbow, 1.0);
draw_set_font(fntNormal);
draw_set_halign(0);draw_set_valign(0);
*/
var _cx = RES_WID >> 1;
var _cy = RES_HEI >> 1;

// 불타는 우리의 고기
shader_set(shd_burn);
// 변수
shader_set_uniform_f(uBurnTime, time);
shader_set_uniform_f(uBurnIntensity, sin(time * 0.02) * 0.5 + 0.5);
texture_set_stage(uBurnMap, texCoolingMap);

// 스프라이트
draw_sprite_ext(sprMEAT, 0, _cx, _cy + 150, 0.75, 0.75, sin(time * 0.1) * 8, c_white, 1.0);
shader_reset();

// 백댄서 준비
draw_sprite_ext(sprDUANEDANCE, time * 0.3, _cx - 250, _cy + 80, 1.5, 1.5, 0, c_white, 1);
draw_sprite_ext(sprDUANESPIN, time * 0.5, _cx + 250, _cy + 80, 1, 1, 0, c_white, 1);

// 오이 타임
draw_set_halign(1);draw_set_valign(1);
draw_set_font(fntLobster);
var _rainbow = make_colour_hsv(time * 2, 255, 255);
draw_text_transformed_colour((RES_WID >> 1), (RES_HEI >> 1) - 150, "Gherkin  Time", 1.5, 1.5, 0, _rainbow, _rainbow, _rainbow, _rainbow, 1.0);
draw_set_font(fntHangul);
draw_set_halign(0);draw_set_valign(0);
surface_reset_target();

#section 불이 그려지는 서피스 업데이트
surface_set_target(surfFire);

// 흰색으로 그리기
d3d_set_fog(true, c_white, 0, 0);
draw_surface(surfGame, 0, 0);
d3d_set_fog(false, 0, 0, 0);

// 마우스로 그리기
var _ratiox = mouse_x / (WIN_WID / RES_WID);
var _ratioy = mouse_y / (WIN_HEI / RES_HEI);
var _subs = 16;
    
if (mouse_check_button(mb_left))
{
    for (var i=0; i<=_subs; i++)
    {
        var _incr = i / _subs;
        draw_circle_colour(lerp(_ratiox, mouse_xprev, _incr), lerp(_ratioy, mouse_yprev, _incr), 16, c_white, c_white, false);
    }
}

mouse_xprev = _ratiox;
mouse_yprev = _ratioy;
surface_reset_target();

#section 불 효과
surface_set_target(surfFire);
shader_set(shd_fire);
// 쉐이더 변수 설정 
shader_set_uniform_f(uTextureSize, RES_WID, RES_HEI);
shader_set_uniform_f(uTime, time);
shader_set_uniform_f(uScrollSpeed, 4);
shader_set_uniform_f(uWindStrength, 1.5);
shader_set_uniform_f(uWindSpeed, 1.5);
texture_set_stage(uCoolingMap, texCoolingMap);
texture_set_stage(uSource, texFire);

// 서피스; 아무 서피스나 써도 됩니다.
// 저희가 필요한건 uv가 있고 화면을 덮는 버텍스면 되니깐요
draw_surface(surfFire, 0, 0);
shader_reset();
surface_reset_target();

// 배경
draw_clear(0);
draw_sprite(bgKot, 0, WIN_WID / 2, WIN_HEI / 2);

// 불
draw_set_blend_mode(bm_add);
shader_set(shd_fire_effect);
draw_surface_stretched(surfFire, 0, 0, WIN_WID, WIN_HEI);
shader_reset();
draw_set_blend_mode(bm_normal);

// 일반 서피스
draw_surface_stretched(surfGame, 0, 0, WIN_WID, WIN_HEI);

// info
draw_text(10, 10, "<엔터 키> 로 다음 데모로 이동#마우스 드래그로 불의구");